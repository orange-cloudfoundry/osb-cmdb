/*
 * Copyright 2002-2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "propdeps-maven"

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->
		// eliminate test-scoped dependencies (no need in maven central poms)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == "test"
		}

		// sort to make pom dependencies order consistent to ease comparison of older poms
		generatedPom.dependencies = generatedPom.dependencies.sort { dep ->
			"$dep.scope:$dep.groupId:$dep.artifactId"
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = "https://spring.io/projects/spring-cloud-app-broker"
			organization {
				name = "Pivotal Software, Inc."
				url = "https://www.spring.io"
			}
			licenses {
				license {
					name "The Apache Software License, Version 2.0"
					url "https://www.apache.org/licenses/LICENSE-2.0.txt"
					distribution "repo"
				}
			}
			scm {
				url = "https://github.com/spring-cloud-incubator/spring-cloud-app-broker"
				connection = "scm:git:https://github.com/spring-cloud-incubator/spring-cloud-app-broker.git"
				developerConnection = "scm:git:https://github.com/spring-cloud-incubator/spring-cloud-app-broker.git"
			}
			developers {
				developer {
					id = "royclarkson"
					name = "Roy Clarkson"
					email = "rclarkson@pivotal.io"
				}
				developer {
					id = "sfrederick"
					name = "Scott Frederick"
					email = "sfrederick@pivotal.io"
				}
			}
			issueManagement {
				system = "GitHub"
				url = "https://github.com/spring-cloud/spring-cloud-app-broker/issues"
			}
		}
	}
}
